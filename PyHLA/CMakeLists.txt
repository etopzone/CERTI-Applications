PROJECT(PyHLA C CXX)
# SET(CMAKE_VERBOSE_MAKEFILE ON)

CMAKE_MINIMUM_REQUIRED(VERSION 2.4)

SET(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} ${CMAKE_INSTALL_PREFIX}/share/scripts)
SET(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} ${CMAKE_CURRENT_SOURCE_DIR})

FIND_PACKAGE(PythonLibs REQUIRED)
FIND_PACKAGE(PythonInterp REQUIRED)

IF(NOT PYTHON_PACKAGES_PATH)
    EXEC_PROGRAM("${PYTHON_EXECUTABLE}"
        ARGS "-c 'from distutils.sysconfig import get_python_lib; print get_python_lib().replace(\"\\\\\",\"/\")'"
        OUTPUT_VARIABLE PYTHON_PACKAGES_PATH)
    MESSAGE(STATUS "Found Python modules: ${PYTHON_PACKAGES_PATH}")
    SET(PYTHON_PACKAGES_PATH "${PYTHON_PACKAGES_PATH}"
        CACHE PATH "Path to Python modules.")
ENDIF(NOT PYTHON_PACKAGES_PATH)

# Use the PYTHON_PACKAGES_PATH as a default CMAKE_INSTALL_PREFIX
IF (CMAKE_INSTALL_PREFIX_INITIALIZED_TO_DEFAULT)
    SET(CMAKE_INSTALL_PREFIX "${PYTHON_PACKAGES_PATH}"
        CACHE PATH "PyHLA install path prefix, prepended onto install directories." FORCE)
ENDIF (CMAKE_INSTALL_PREFIX_INITIALIZED_TO_DEFAULT)
MESSAGE(STATUS "Python modules will be installed in: ${CMAKE_INSTALL_PREFIX}")

ADD_SUBDIRECTORY(hla)
ADD_SUBDIRECTORY(test)

SET(CPACK_PACKAGE_DESCRIPTION_SUMMARY "Python bindings for the Modeling and Simulation (M&S) High Level Architecture (HLA)")
SET(CPACK_PACKAGE_VENDOR "Petr Gotthard")
SET(CPACK_RESOURCE_FILE_LICENSE "${CMAKE_CURRENT_SOURCE_DIR}/COPYING")
SET(CPACK_PACKAGE_NAME PyHLA)
SET(CPACK_SYSTEM_NAME ${CMAKE_SYSTEM_NAME})

IF (WIN32)
    SET(CPACK_SOURCE_GENERATOR "ZIP")
    SET(CPACK_GENERATOR "NSIS;ZIP")
ELSE (WIN32)
    SET(CPACK_SOURCE_GENERATOR "TGZ;ZIP")
    SET(CPACK_GENERATOR "TGZ;ZIP;RPM")
ENDIF (WIN32)

SET(CPACK_SOURCE_IGNORE_FILES
    "~" ".pdf" ".pyc" ".so" ".dll" ".zip" ".tar.gz" ".rpm"
    "/.cvsignore" "/Makefile" "/CMakeCache.txt" "/cmake_install.cmake" "/CPack.*.cmake"
    "/CVS/" "/CMakeFiles/" "/_CPack_Packages/")

INCLUDE(CPack)

# $Id: CMakeLists.txt,v 1.6 2008/10/28 21:13:19 gotthardp Exp $
